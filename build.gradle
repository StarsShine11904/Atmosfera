plugins {
    id 'fabric-loom' version '1.9-SNAPSHOT'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
}

version = project.mod_version + '+mc' + project.minecraft_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    maven {
        name = 'ModMenu'
        url = 'https://maven.terraformersmc.com/releases/'
    }
    maven {
        name = 'ClothConfig'
        url = 'https://maven.shedaniel.me/'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation ("com.terraformersmc:modmenu:${project.modmenu_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    modApi("me.shedaniel.cloth:cloth-config-fabric:${project.clothconfig_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    def stringProperties = project.properties.findAll { it.value instanceof String }

    inputs.properties(stringProperties)

    filesMatching(["*.mixins.json", "*.mod.json", "**/pack.mcmeta"]) {
        expand(stringProperties) {
            escapeBackslash = true
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.compilerArgs.add('-Xlint:deprecation')
    it.options.release = project.java_version as Integer
}

java {
    targetCompatibility = sourceCompatibility = project.java_version as Integer
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

def changesMd = file('changes.md').getText('UTF-8')
def toList = (String s) -> s.split(',').collect { it.trim() }
def gameVersionList = toList(project.game_version_list)

modrinth {
    def MODRINTH_TOKEN = System.getenv('MODRINTH_TOKEN')
    if (MODRINTH_TOKEN != null) {
        token = MODRINTH_TOKEN
        projectId = project.modrinth_id
        versionName = project.version
        versionNumber = project.version
        versionType = 'release'
        uploadFile = remapJar
        changelog = changesMd
        gameVersions = gameVersionList
        loaders = ['fabric']
        dependencies {
            toList(project.modrinth_dependencies).each { required.project(it) }
            toList(project.modrinth_optional_dependencies).each { optional.project(it) }
        }
    }
}

tasks.modrinth.dependsOn("build")

curseforge {
    def CURSEFORGE_TOKEN = System.getenv('CURSEFORGE_TOKEN')
    if (CURSEFORGE_TOKEN != null) {
        apiKey = CURSEFORGE_TOKEN
        project {
            id = project.curseforge_id
            releaseType = 'release'
            changelogType = 'markdown'
            changelog = changesMd

            addGameVersion 'Fabric'
            gameVersionList.each { addGameVersion(it) }

            relations {
                toList(project.curseforge_dependencies).each { requiredDependency(it) }
                toList(project.curseforge_optional_dependencies).each { optionalDependency(it) }
            }
            mainArtifact(remapJar.archiveFile.get().asFile) {
                displayName = "${project.mod_version} [${versionRange(gameVersionList)}]"
            }
            afterEvaluate {
                uploadTask.dependsOn('build')
            }
        }
        options {
            forgeGradleIntegration = false
            javaVersionAutoDetect = false
        }
    }
}

static String versionRange(List<String> gameVersions) {
    if (gameVersions.size() == 1)
        return gameVersions[0]

    def v0 = gameVersions.first()
    def v1 = gameVersions.last()

    def s0 = v0.split('[.]').collect { it as Integer }
    def s1 = v1.split('[.]').collect { it as Integer }

    // e.g. 1.19.3, 1.19.4 -> 1.19.3/4
    if (s0.size() == 3 && s1.size() == 3 && s0[0] == s1[0] && s0[1] == s1[1] && s1[2] - s0[2] == 1) {
        return "${s1[0]}.${s1[1]}.${s0[2]}/${s1[2]}"
    }

    return "${v0} - ${v1}"
}
